buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'org.gretty:gretty:4.0.3'
    }
}

apply plugin: 'java'
apply plugin: 'org.gretty'

archivesBaseName = 'androidemu_demo'

java {
    sourceCompatibility = 11
    targetCompatibility = 11
}

dependencies {
    implementation 'org.gwtproject:gwt-user:2.10.0'
    implementation 'org.gwtproject:gwt-dev:2.10.0'
    implementation project(':androidemu')
    implementation project(path: ':androidemu', configuration: 'archives')
}

clean.doFirst {
    delete "${project.projectDir}/src/main/gwt-unitCache"
    delete "${project.projectDir}/src/main/webapp/WEB-INF"
    delete "${project.projectDir}/src/main/webapp/demo"
}

task compileGwt (dependsOn: classes, type: JavaExec) {
    ext.buildDir = "${project.projectDir}/src/main/webapp"
    ext.extraDir = "${project.buildDir}/extra"

    sourceSets.main.java.srcDirs.each{ dir ->
        inputs.dir dir
    }
    inputs.dir sourceSets.main.output.resourcesDir
    outputs.dir buildDir

    doFirst {
        file(buildDir).mkdirs()
    }

    mainClass = 'com.google.gwt.dev.Compiler'

    classpath {
        [
                sourceSets.main.java.srcDirs,           // Java source
                sourceSets.main.output.resourcesDir,    // Generated resources
                sourceSets.main.java.classesDirectory,  // Generated classes
                sourceSets.main.compileClasspath,       // Deps
        ]
    }

    args =
        [
                'android.demo',
                '-war', buildDir,
                '-extra', extraDir,
                '-logLevel', 'INFO',
                '-localWorkers', '2',
                '-style', 'PRETTY',
                '-XdisableCastChecking',   // Disables runtime checking of cast operations. This can reduce compiled file sizes.
                '-XdisableClassMetadata',  // Disables some java.lang.Class methods. This can reduce compiled file sizes.
        ]

    maxHeapSize = '512M'
}

jar.dependsOn compileGwt

gretty {
    servletContainer = 'tomcat10'
    contextPath = '/gwt-android-emu'
}